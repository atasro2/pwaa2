gNumMusicPlayers = 4;
gMaxLines = 0;
SECTIONS {
    . = 0x2000000;
    ewram (NOLOAD) :
    ALIGN(4)
    {
        INCLUDE "sym_ewram.txt"
    }
    . = 0x3000000;
    iwram (NOLOAD) :
    ALIGN(4)
    {
        INCLUDE "sym_iwram.txt"
    }
    . = 0x8000000;
    .text :
    ALIGN(4)
    {
        src/crt0.o(.text);
        src/main.o(.text);
        asm/bg.o(.text);
        src/utils.o(.text);
        src/case_data.o(.text);
        src/investigation_segments.o(.text);
        src/script_main.o(.text);
        asm/script_commands.o(.text);
        asm/romA7A8.o(.text);
        src/sound.o(.text);
        asm/animation.o(.text);
        asm/rom15CE0.o(.text);
        /* library functions */
        src/m4a_1.o(.text);
        src/m4a.o(.text);
        src/libagbsyscall.o(.text);
        src/agb_sram.o(.text);
        *libgcc.a:*_call_via_rX*.o(.text*);
        *libgcc.a:*_divsi3*.o(.text*);
        *libgcc.a:*_dvmd_tls*.o(.text*);
	    *libgcc.a:*_modsi3*.o(.text*);        
	    *libgcc.a:*_udivsi3*.o(.text*);
        *libgcc.a:*_umodsi3*.o(.text*);
    } =0
    .rodata :
    ALIGN(4)
    {
        src/main.o(.rodata);
	    rodata/bg.o(.rodata);
	    src/utils.o(.rodata);
	    rodata/case_data.o(.rodata);
	    src/investigation_segments.o(.rodata);
	    rodata/investigation_segments.o(.rodata);
	    rodata/script_main.o(.rodata);
	    rodata/script_commands.o(.rodata);
	    rodata/romA7A8.o(.rodata);
	    rodata/animation.o(.rodata);
	    rodata/rom15CE0.o(.rodata);
        src/m4a_tables.o(.rodata);
        rodata/sound.o(.rodata);
        src/agb_sram.o(.rodata); 
    } =0
    /* capcom :^)) */
    . += 0x100;
    .data :
    ALIGN(4)
    {
        data/main.o(.data);
        data/bg.o(.data);
        src/utils.o(.data);
        data/case_data.o(.data);
        data/script_main.o(.data);
        data/romA7A8.o(.data);
        src/sound.o(.data);
        data/animation.o(.data);
        data/rom15CE0.o(.data);
    } =0

    . = 0x8130000;
    gfx_data :
    ALIGN(4)
    {
        data/graphics.o(gfx_data);
    } =0

    /* DWARF debug sections.
    Symbols in the DWARF debugging sections are relative to the beginning
    of the section so we begin them at 0.  */

    /* DWARF 1 */
    .debug          0 : { *(.debug) }
    .line           0 : { *(.line) }

    /* GNU DWARF 1 extensions */
    .debug_srcinfo  0 : { *(.debug_srcinfo) }
    .debug_sfnames  0 : { *(.debug_sfnames) }

    /* DWARF 1.1 and DWARF 2 */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }

    /* DWARF 2 */
    .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }

    /* Discard everything not specifically mentioned above. */
    
	/DISCARD/ :
    {
        *(*);
    }
	
}
